ProgDeclaration -> DeclarationList

DeclarationList -> Declaration DeclarationList
DeclarationList -> ''

Declaration -> VariableDeclaration
Declaration -> FunctionDeclaration

VariableDeclaration -> Type AtomicDeclarationList semi_colon

AtomicDeclarationList -> AtomicDeclaration comma AtomicDeclarationList
AtomicDeclarationList -> ''

AtomicDeclaration -> identifier assign Expression

FunctionDeclaration -> kw_fn identifier lparen ArgList rparen Type lbrace StmtList rbrace

ArgList -> Type identifier comma ArgList
ArgList -> ''

Expression -> Term ExpressionRHS

ExpressionRHS -> bin_op Expression
ExpressionRHS -> ''

Term -> lparen Expression rparen
Term -> int_literal
Term -> float_literal
Term -> str_literal
Term -> bool_literal

Term -> IdentifierOrCallTerm
IdentifierOrCallTerm -> identifier Call 
Call -> lparen FormalArgList rparen
Call -> ''

FormalArgList -> Expression comma FormalArgList
FormalArgList -> ''


StmtList -> Stmt semi_colon StmtList
StmtList -> ''

Stmt -> VariableDeclaration
Stmt -> Assignment
Stmt -> Conditional
Stmt -> Loop
Stmt -> Return
Stmt -> Expression
Stmt -> lbrace StmtList rbrace


Assignment -> identifier assign Expression

Conditional -> IfBlock ElifBlocks ElseBlock

IfBlock -> kw_if Expression lbrace StmtList rbrace

ElifBlocks -> ElifBlock ElifBlocks
ElifBlocks -> ''

ElifBlock -> kw_elif Expression lbrace StmtList rbrace

ElseBlock -> kw_else lbrace StmtList rbrace
ElseBlock -> ''

Loop -> ForLoop
Loop -> WhileLoop

ForLoop -> kw_for identifier kw_in LoopRange lbrace StmtList rbrace

LoopRange -> int_literal range int_literal LoopStep
LoopStep -> range int_literal
LoopStep -> ''

WhileLoop -> kw_while Expression lbrace StmtList rbrace

Return -> kw_return ReturnVariable
ReturnVariable -> Expression
ReturnVariable -> ''

Type -> atomic_type
Type -> lbracket int_literal rbracket Type